//
// Copyright (c) 2007 Tridium, Inc
// Licensed under the Academic Free License version 3.0
//
// History:
//   10 Jan 07  Andy Saunders  Creation
//   27 Apr 07  Brian Frank    Port from Java to Sedona
//

**
** Avg Running Average math object
**
** out = (sum last 10 value)/10
**
@niagaraIcon="module://icons/x16/control/math/average.png"
class Avg10
  extends Component
{

  property float out
  property float in
  property int   maxTime

  virtual override void execute()
  {
    float cv = in
    if (index < 0)
    {
      for (int i = 0; i < 10; i++)
        samples[i] = cv
      total = cv * 10.0f
      index = 0
      lastValue = cv
    }
    else
    {
      bool forceSample = false
      if(maxTime > 0)
      {
        forceSample = (Sys.ticks() - lastChangeTime >= (long)maxTime * 1ms)
      }
      if (cv != lastValue || forceSample)
      {
        total = total - samples[index] + cv
        samples[index] = cv
        if (++index >= 10) index = 0
        lastValue = cv
        lastChangeTime = Sys.ticks()
        //Sys.out.print("cv=").printFloat(cv).print(" total=").printFloat(total).print(" avg = ").printFloat(total/10.0f).nl()
      }
    }
    out = total / 10.0f
  }

  float total = 0.0
  float lastValue = 0.0
  int index = -1
  long lastChangeTime = 0L
  inline float[10] samples

}
