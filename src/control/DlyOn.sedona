//
// Copyright (c) 2007 Tridium, Inc
// Licensed under the Academic Free License version 3.0
//
// History:
//   10 Jan 07  Andy Saunders  Creation
//   27 Apr 07  Brian Frank    Port from Java to Sedona
//

**
** DlyOn Delay On object
**
**    Delays the off to on transition by a time delay
**
@niagaraIcon="module://icons/x16/control/control.png"
class DlyOn
  extends Component
{

  property bool out = true
  property bool in
  property float delayTime  // specified hold time in sec
  property int hold         // remaining hold time in ms

  virtual override void execute()
  {
    bool cur = in
    bool curOut = out
    long now = Sys.ticks()
    if(state == 0)  // hold is not active
    {
      if(cur)
      {
        // start delay
        long delayTimeInt = (long)(delayTime * 1000.0f)
        holdTime = now + (1ms * delayTimeInt)
        //Sys.log("startingOnDelay  = ").print(holdTime).nl();
        state = 1
      }
      else if(curOut)
      {
        //Sys.log("clearing out state 0").nl();
        out = false
      }

    }
    else if(state == 1)  // hold is currently active
    {
      if(now > holdTime)
      {
        //Sys.log("State 1 delay Expired").nl();
        out = true
        state = 2
        holdTime = 0ms
      }
    }
    else if(state == 2)  // hold just expired
    {
      if(!cur)
      {
        //Sys.log("State 2 !in so clear out").nl();
        out = false
        state = 0
      }
    }
    if(holdTime == 0L)
      hold = 0
    else
     hold = (int)(holdTime - now)

  }

  private int state     = 0
  private long holdTime = 0L
}
